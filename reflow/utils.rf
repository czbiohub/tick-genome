// Instantiate the system modules "dirs" (system modules begin
// with $), assigning its instance to the "dirs" identifier. To
// view the documentation for this module, run "reflow doc
// $/dirs".
val dirs = make("$/dirs")
val files = make("$/files")
val path = make("$/path")

val tick_genome = "czbiohub/tick-genome"

// Instantiate Go system module "strings"
val strings = make("$/strings")

func CopyRenamedDir(results dir, id, output string) = {                                                                                                                                                                                                                          
  r := map([(id + "_" + filename, filepointer)| (filename, filepointer) <- map(results)])                                                                                                                                                                                                                       
  d := dirs.Make(r)                                                                                                                                                                                                                                                           
  dirs.Copy(d, output)                                                                                                                                                                                                                                                        
}  

func CopyRenamedFile(results file, id, outdir, suffix string) = {
	basename := id + suffix
	renamed := path.Join([outdir, basename])
	files.Copy(results, renamed)
}  

func SplitByPipe(pipe_separated string) = {
	// Split each read string by the pipe "|" to get individual s3 paths
	val split = strings.Split(pipe_separated, "|")

	// Create a file for each element in the split string array
	// Now everything is an array of files
	[file(filename) | filename <- split]
}


func ParallelUngzip(gzipped file, threads int) =
    exec(image := tick_genome, cpu := threads) (unzipped file) {"
    	pigz --processes {{threads}} -dc {{gzipped}} > {{unzipped}}
"}


func ParallelGzip(filename file, threads int) =
    exec(image := tick_genome, cpu := threads) (gzipped file) {"
    	pigz --processes {{threads}} -c {{filename}} > {{gzipped}}
"}

// Take dirs.Pick() items and turn them into a directory
func MakeReportDir(items [(file, string)]) = {
	// dirs.Pick outputs (file, string) for the file and filename,
	// but we need (string, file) with the filename and file for dirs.Make
	swapped := [(name, f) | (f, name) <- items]
	dirs.Make(map(swapped))
}

// Add a status to the name of the reads
func MakeNamedStatusReads(r1, r2 file, id, status string) = 
	[id + "_R1" + status: r1, id + "_R2" + status: r2]
